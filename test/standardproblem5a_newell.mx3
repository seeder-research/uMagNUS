/*
	Test for reversal driven by Slonczewski STT.
*/

Nx := 64
Ny := 32
Nz := 1

sX := 160e-9
sY := 80e-9
sZ := 5e-9
 
setgridsize(Nx, Ny, Nz)
setcellsize(sX/Nx, sY/Ny, sZ/Nz)

Msat  = 800e3
Aex   = 13e-12
alpha = 3

EnableDemag = false
EnableNewellDemag = true

// Set a initial magnetisation to C-state
m = uniform(1, 1, 0.001)

minimize()

alpha        = 0.01
lambda       = 1
Pol          = 0.5669
epsilonprime = 0

pdeg := 1    
prad := pdeg * pi / 180.0
px := cos(prad)
py := sin(prad)
fixedlayer = vector(px, py, 0)

Jtot := -0.008  // total current in amps
carea := sX * sY
jc := Jtot / carea  
print("Current density is: " ,  jc)
 
J = vector(0, 0, jc)

//autosave(m, 1e-12)
tableautosave(10e-12)

save(sttorque)
TOL := 1e-3

m0 := m.average()
expectv("m", m0, vector(0.9585264325141907, 0.20422574877738953, 0), TOL)

run(1e-9)
m1 := m.average()
expectv("m", m1, vector(0.6427677195465802, 0.5114272236824036, -0.1541234254837036), TOL)

run(1e-9)
m2 := m.average()
expectv("m", m2, vector(-0.9572336673736572, 0.2079927921295166, 0.009614836424589157), TOL)

